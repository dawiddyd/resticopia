plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.8.22'
}

def getVersionCodeOrNull = { ->
    def versionCode = System.getenv('GIT_COMMIT_COUNT')?.toInteger()
    if (versionCode != null) versionCode + 1941588398
    else null
}

def getVersionOrNull = { ->
    // First try CI_VERSION environment variable
    def matcher = System.getenv('CI_VERSION') =~ /refs\/tags\/(.*)/
    if (matcher.find()) return matcher.group(1)
    
    // If not in CI, try to get version from git describe
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--exact-match'
            standardOutput = stdout
            ignoreExitValue = true
        }
        def version = stdout.toString().trim()
        if (version && !version.isEmpty()) return version
    } catch (Exception e) {
        // Ignore and fall through
    }
    
    return null
}

android {
    compileSdkVersion 33
    defaultConfig {
        applicationId "org.dydlakcloud.resticopia"
        minSdkVersion 24
        targetSdkVersion 33
        versionCode getVersionCodeOrNull() ?: 1
        versionName getVersionOrNull() ?: "0.0.1-SNAPSHOT"
        setProperty('archivesBaseName', [
                "resticopia",
                versionName
        ].findAll({ it != null }).join("-"))

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            storeFile file(System.env.RELEASE_STORE_FILE ?: "null")
            storePassword System.env.RELEASE_STORE_PASSWORD
            keyAlias System.env.RELEASE_KEY_ALIAS
            keyPassword System.env.RELEASE_KEY_PASSWORD
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
    packagingOptions {
        jniLibs {
            useLegacyPackaging true
        }
    }
    splits {
        abi {
            enable true
            universalApk true
        }
    }
    namespace 'org.dydlakcloud.resticopia'
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.3'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.6.0"
    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.6.0'
    implementation 'androidx.navigation:navigation-ui-ktx:2.6.0'
    implementation "androidx.security:security-crypto:1.1.0-alpha06"
    
    // Unit Testing Dependencies
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'io.mockk:mockk:1.13.8'
    testImplementation 'io.mockk:mockk-android:1.13.8'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.7.3'
    testImplementation 'app.cash.turbine:turbine:1.0.0'
    testImplementation 'com.google.truth:truth:1.1.5'
    testImplementation 'androidx.arch.core:core-testing:2.2.0'
    testImplementation 'org.robolectric:robolectric:4.11.1'
    
    // Android Testing Dependencies
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'io.mockk:mockk-android:1.13.8'
}